plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
    id 'io.codearte.nexus-staging' version '0.20.0'
}

subprojects {

    apply plugin: 'groovy'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    scmVersion {
        tag {
            prefix = ''
        }
    }

    group = 'com.github.mkopylec'
    archivesBaseName = name
    version = scmVersion.version

    repositories {
        mavenCentral()
    }

    ext {
        springBootVersion = '2.1.3.RELEASE'
        resilience4jVersion = '0.15.0'
        okHttpVersion = '3.14.2'
        
        ossrhUsername = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
        ossrhPassword = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
        
        pomContent = {
            name = 'Charon Spring Boot Starter'
            description = 'Reverse proxy implementation in form of Spring Boot starter'
            url = 'https://github.com/mkopylec/charon-spring-boot-starter'
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id = 'mkopylec'
                    name = 'Mariusz Kopylec'
                    email = 'mariusz.kopylec@o2.pl'
                }
            }
            scm {
                connection = 'scm:git:https://github.com/mkopylec/charon-spring-boot-starter.git'
                developerConnection = 'scm:git:https://github.com/mkopylec/charon-spring-boot-starter.git'
                url = 'https://github.com/mkopylec/charon-spring-boot-starter'
            }
        }
    }

    dependencies {
        compile group: 'org.springframework', name: 'spring-web', version: '5.1.5.RELEASE'
        compile group: 'io.github.resilience4j', name: 'resilience4j-circuitbreaker', version: resilience4jVersion
        compile group: 'io.github.resilience4j', name: 'resilience4j-ratelimiter', version: resilience4jVersion
        compile group: 'io.github.resilience4j', name: 'resilience4j-retry', version: resilience4jVersion
        compile group: 'io.github.resilience4j', name: 'resilience4j-micrometer', version: resilience4jVersion
        compile group: 'io.micrometer', name: 'micrometer-core', version: '1.1.3'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
        compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'
        compile group: 'commons-io', name: 'commons-io', version: '2.6'
    }

    signing {
        if (project.ext.has('signArtifacts')) {
            sign configurations.archives
        }
    }
}

wrapper {
    gradleVersion = '5.4.1'
}
